# Shortcut to this dotfiles path and other...
export DOTFILES=$HOME/.dotfiles
export DEVFOLDER=~/Code

# Source manjaro-zsh-configuration
if [[ -e /usr/share/zsh/manjaro-zsh-config ]]; then
    source /usr/share/zsh/manjaro-zsh-config
fi
# Use manjaro zsh prompt
if [[ -e /usr/share/zsh/manjaro-zsh-prompt ]]; then
    source /usr/share/zsh/manjaro-zsh-prompt
fi

# For private configuration, wink wink
if [[ -a ~/.localrc ]]
then
    source ~/.localrc
fi

# All of our zsh files
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# Load the path files
for file in ${(M)config_files:#*/path.zsh}
do
    source $file
done

# Load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
    source $file
done

# Initialize autocomplete here
autoload -U compinit
compinit

# Load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
    source $file
done

unset config_files

export GPG_TTY=$(tty)
